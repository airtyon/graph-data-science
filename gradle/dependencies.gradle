ext {
    neos = [
        '4.4'           : properties.getOrDefault('neo4jVersion44', '4.4.17'),
        '5.1'           : properties.getOrDefault('neo4jVersion51', '5.1.0'),
        '5.2'           : properties.getOrDefault('neo4jVersion52', '5.2.0'),
        '5.3'           : properties.getOrDefault('neo4jVersion53', '5.3.0'),
        '5.4'           : properties.getOrDefault('neo4jVersion53', '5.4.0'),
    ]

    neo4jDefault = neos.'4.4'
    neo4j_minor = properties.getOrDefault('neo4jVersion', neo4jDefault).toString().split('\\.')[0..1].join('.')

    scalas = [
        '4.3': '2.12.13',
        '4.4': '2.12.13',
        '5.0': '2.13.8',
        '5.1': '2.13.8',
        '5.2': '2.13.8',
        '5.3': '2.13.8',
        '5.4': '2.13.8',
        '5.5': '2.13.8',
    ]

    ver = [
        doc                    : [
            'asciidoctorj'        : '2.5.6',
            'reflections'         : '0.10.2'
        ],
        'assertj'              : '3.23.1',
        'auto-common'          : '1.1.2',
        'auto-service'         : '1.0',
        'bouncycastle-prov'     : '1.71',
        'checkStyle'            : '10.3.4',
        'commons-compress'     : '1.21',
        'commons-io'           : '2.11.0',
        'commons-lang3'        : '3.12.0',
        'commons-math3'        : '3.6.1',
        'commons-text'         : '1.10.0',
        'compile-testing'      : '0.19',
        'cypher-dsl'           : '2021.2.0',
        'eclipse-collections'  : '10.4.0',
        'ejml'                 : '0.41',
        'fastcsv'              : '1.0.4',
        'gdl'                  : '0.3.8',
        'hamcrest'             : '2.2',
        'HdrHistogram'         : '2.1.12',
        'hppc'                 : '0.7.3',
        'immutables'           : '2.8.8',
        'ipaddress'            : '5.3.3',
        'jackson'              : '2.14.1',
        'javapoet'             : '1.13.0',
        'jetbrains-annotations': '23.0.0',
        'jjwt'                 : '0.11.5',
        'jol'                  : '0.16',
        'jqwik'                : '1.6.5',
        'junit5bom'            : '5.9.1',
        'memoryfilesystem'     : '2.2.0',
        'mockito'              : '4.11.0',
        'mockito-junit-jupiter': '4.11.0',
        'neo4j'                : properties.getOrDefault('neo4jVersion', neo4jDefault),
        'opencypher-front-end' : '9.0.20210312',
        'picocli'              : '4.6.1',
        'qala-datagen'         : '2.3.0',
        'RoaringBitmap'        : '0.9.32',
        'sb-contrib'           : '7.4.7',
        'scala'                : scalas[neo4j_minor],
        'spotbugsToolVersion'  : '4.7.2',
        'protobuf'             : '3.21.10',
    ]
    dep = [
        junit5bom:          "org.junit:junit-bom:${ver.junit5bom}",
        junit5jupiter:      "org.junit.jupiter:junit-jupiter",
        junit5platform:     "org.junit.platform:junit-platform-launcher",
        junit5migration:    "org.junit.jupiter:junit-jupiter-migrationsupport",
    ]
}

def neo_deps = [
    'annotations',
    'neo4j-bolt',
    'neo4j-collections',
    'neo4j-command-line',
    'neo4j-common',
    'neo4j-concurrent',
    'neo4j-configuration',
    'neo4j-diagnostics',
    'neo4j-fabric',
    'neo4j-graphdb-api',
    'neo4j-id-generator',
    'neo4j-import-util',
    'neo4j-index',
    'neo4j-io',
    'neo4j-kernel',
    'neo4j-kernel-api',
    'neo4j-layout',
    'neo4j-lock',
    'neo4j-logging',
    'neo4j-monitoring',
    'neo4j-procedure-api',
    'neo4j-record-storage-engine',
    'neo4j-resource',
    'neo4j-schema',
    'neo4j-ssl',
    'neo4j-storage-engine-api',
    'neo4j-token-api',
    'neo4j-unsafe',
    'neo4j-values',
    'neo4j',
]

def neo_deps_50 = neo_deps - ['neo4j-storage-engine-api', 'neo4j-cypher-runtime-util']

ext.neodeps = {
    if (ver.'neo4j'.startsWith('5.')) {
        neo_deps_50.iterator()
    } else {
        neo_deps.iterator()
    }
}
